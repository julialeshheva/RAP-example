@Metadata.layer: #CORE
@UI: {
  headerInfo: { typeName: 'Travel',
                typeNamePlural: 'Travels',
                title: { type: #STANDARD, label: 'Travel', value: 'TravelID' } },
  presentationVariant: [{ sortOrder: [{ by: 'TravelID', direction:  #DESC }] }] }
annotate view ZC_UI_TRAVEL
    with 
{
  @UI.facet: [ { id:              'Travel', // define the navigation to the object page and its layout
                 purpose:         #STANDARD,
                 type:            #IDENTIFICATION_REFERENCE,
                 label:           'Travel',
                 position:        10 },
               { id:              'Booking',
                 purpose:         #STANDARD,
                 type:            #LINEITEM_REFERENCE,
                 label:           'Booking',
                 position:        20,
                 targetElement:   '_Booking'} ] 
  @UI:{ identification: [{ position: 1, label: 'Travel UUID' }] }  // specify default position and eventually a label                
  TravelUUID; 
  
  @UI: {  lineItem:       [ { position: 10 } ], // is used to specify the layout information of each element shown as a column in the list report
          identification: [ { position: 10 } ], // is used to specify the layout information of each element shown in the identification section of the Object Page
          selectionField: [ { position: 10 } ] } // is used to enable an element for selection in the filter bar
  TravelID;
  
  @UI: {  lineItem:       [ { position: 20 } ],
          identification: [ { position: 20 } ],
          selectionField: [ { position: 20 } ] }  
  AgencyID;
  //AgencyName;
  
  @UI: {  lineItem:       [ { position: 30 } ],
          identification: [ { position: 30 } ],
          selectionField: [ { position: 30 } ] }  
  CustomerID;
//  CustomerName;

  @UI: {  lineItem:       [ { position: 40 } ],
          identification: [ { position: 40 } ] }
  BeginDate;
  
  @UI: {  lineItem:       [ { position: 50 } ],
          identification: [ { position: 50 } ] }  
  EndDate;
   @UI: {  lineItem:       [ { position: 60 } ],
          identification: [ { position: 60 } ] }
  BookingFee;

  @UI: {  lineItem:       [ { position: 70 } ],
          identification: [ { position: 70 } ] }
  TotalPrice;
  //  CurrencyCode;

  @UI: {  lineItem:       [ { position: 80 } ],
          identification: [ { position: 80 } ] }
  Description; 

  @UI: {  lineItem:       [ { position: 90 },
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' }
                         ],
         identification: [ { position: 90 },
                           { type: #FOR_ACTION, dataAction: 'acceptTravel', label: 'Accept Travel' },
                           { type: #FOR_ACTION, dataAction: 'rejectTravel', label: 'Reject Travel' }
                         ] }
  TravelStatus;
  
  @UI.hidden: true   
  CreatedBy;
  
  @UI.hidden: true 
  CreatedAt;
  
  @UI.hidden: true 
  LastChangedBy;

  @UI.hidden: true  
  LastChangedAt;
  
  @UI.hidden: true
  LocalLastChangedAt;
  /* Associations 
  _Agency;
  _Booking;
  _Currency;
  _Customer;*/
    
}

/* Some of the Consumption annotations defined in Projection CDS make the need 
for UI annotations redundant. This is the case for the elements AgencyName, AgencyID. AgencyName is the textual descriptions for Agency ID.
CurrencyCOde is the refference field for the BookingFee and TotalPrice

These elements will be displayed together*/
