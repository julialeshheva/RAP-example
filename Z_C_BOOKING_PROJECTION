@EndUserText.label: 'Booking Projection View'
@AccessControl.authorizationCheck: #CHECK
@Metadata.allowExtensions: true
@Search.searchable: true
define view entity ZC_IU_BOOKING 
  as projection on ZI_IU_BOOKING as  Booking
{
    key BookingUUID,
    TravelUUID,
    
    @Search.defaultSearchElement: true
    BookingID,
    
    BookingDate,
    
    @Consumption.valueHelpDefinition: [{ entity : { name: '/DMO/I_Customer', element: 'CustomerID' } }]
    @ObjectModel.text.element: [ 'CustomerName' ]
    @Search.defaultSearchElement: true
    CustomerID,
    
    _Customer.LastName as CustomerName,
    
    @Consumption.valueHelpDefinition: [{ entity : { name: '/DMO/I_Carrier', element: 'AirlineID' } }]
    @ObjectModel.text.element: [ 'CarrierName' ]    
    CarrierID,   
    _Carrier.Name as CarrierName,
    
    @Consumption.valueHelpDefinition: [ {entity: {name: '/DMO/I_Flight', element: 'ConnectionID'},   // additional binding condition is defined for returning values from the selected value help record for the local view elements CarrierId, FlightDate, FlightPrice, and Currency
                                         additionalBinding: [ { localElement: 'CarrierID',    element: 'AirlineID' },
                                                              { localElement: 'FlightDate',   element: 'FlightDate',   usage: #RESULT},
                                                              { localElement: 'FlightPrice',  element: 'Price',        usage: #RESULT },
                                                              { localElement: 'CurrencyCode', element: 'CurrencyCode', usage: #RESULT } ] } ]  
    ConnectionID,
    FlightDate,
    
    @Semantics.amount.currencyCode: 'CurrencyCode'
    FlightPrice,
    
    @Consumption.valueHelpDefinition: [{ entity : { name: 'I_Currency', element: 'Currency' } }]    
    CurrencyCode,
    CreatedBy,
    LastChangedBy,
    LocalLastChangedAt,
    
    
    /* Associations */
    // the parent BO association has been redirected to the Travel BO Projection
    _Travel : redirected to parent ZC_UI_TRAVEL,   
     
    _Carrier,
    _Connection,
    _Currency,
    _Customer,
    _Flight

}
